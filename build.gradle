/*
 * Copyright 2009-2019 Patrick Mairif.
 * The program is distributed under the terms of the Apache License (ALv2).
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'de.highbyte_le.semver' version '1.3.1'
}

import de.highbyte_le.semver.ReadSemVer

sourceCompatibility = 1.8

project.group='com.github.pmairif'
version = new ReadSemVer(project).read()


ext {
    versionJunit5 = "5.5.2"
    versionSlf4j = "1.7.+"
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation "org.slf4j:slf4j-api:$versionSlf4j"

    testImplementation "org.junit.jupiter:junit-jupiter:${versionJunit5}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${versionJunit5}"
    testImplementation "org.slf4j:slf4j-simple:$versionSlf4j"
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "jcprogress",
                "Implementation-Version": project.version
        )
    }
    metaInf {
        from ('.') {
            include 'LICENSE', 'NOTICE'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

ext.mavenUrl = version.endsWith('-SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2"

publishing {
    repositories {
        maven {
            url mavenUrl
            credentials {
                username sonatypeUser
                password sonatypePass
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'jcprogress'
                description = 'Java Console Progress Indicator. Provides a java library for easily displaying different progress indicators on the console.'
                url = 'https://github.com/pmairif/jcprogress'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pmairif'
                        name = 'Patrick Mairif'
                        email = 'pm@mairif.biz'
                    }
                }
                scm {
                    connection = 'git@github.com:pmairif/jcprogress.git'
                    developerConnection = 'git@github.com:pmairif/jcprogress.git'
                    url = 'https://github.com/pmairif/jcprogress'
                }
                issueManagement {
                    url = 'https://github.com/pmairif/jcprogress/issues'
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

signing {
    if (!version.endsWith('-SNAPSHOT')) {
        sign publishing.publications.mavenJava
    }
}
